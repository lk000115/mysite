```
 *&---------------------------------------------------------------------*
*& Report ZTEST_LK_04
*&---------------------------------------------------------------------*
*& 测试客户主数据导入程序 zsd001
*&---------------------------------------------------------------------*
REPORT ztest_lk_04   MESSAGE-ID ZMM01 .

*----------------------------------------------------------------------*
*  表声明
*----------------------------------------------------------------------*
TABLES:SSCRFIELDS.
*----------------------------------------------------------------------*
* ALV参数声明
*----------------------------------------------------------------------*
DATA:GT_FIELDCAT TYPE LVC_T_FCAT, " 字段目录内表
     GS_FIELDCAT TYPE LVC_S_FCAT, " 字段目录工作区
     LAYOUT      TYPE LVC_S_LAYO. " ALV布局
*----------------------------------------------------------------------*
* 数据类型
*----------------------------------------------------------------------*
TYPES:BEGIN OF TY_INPUT,
        BU_GROUP   TYPE TB001-BU_GROUP,     " 业务伙伴分组
        BU_PARTNER TYPE BU_PARTNER,         " 合作伙伴号
        BPEXT      TYPE BUT000-BPEXT,       " 外部客户号
        NAME_ORG1  TYPE BUT000-NAME_ORG1,   " 名称1
        NAME_ORG2  TYPE BUT000-NAME_ORG2,   " 名称2
        NAME_ORG3  TYPE BUT000-NAME_ORG3,   " 名称3
        NAME_ORG4  TYPE BUT000-NAME_ORG4,   " 名称4
        BU_SORT1   TYPE BUT000-BU_SORT1,    " 搜索项1
        BU_SORT2   TYPE BUT000-BU_SORT2,    " 搜索项2
        POST_CODE1 TYPE AD_PSTCD1,          " 邮政编码
        REGION     TYPE REGIO,              " 省份/地区
        CITY       TYPE AD_CITY1,           " 城市
        COUNTRY    TYPE LAND1,              " 国家
        STREET1    TYPE ADRC-STREET,        " 街道1
        STREET2    TYPE ADRC-STR_SUPPL1,    " 街道2
        LANGU      TYPE SY-LANGU,           " 语言
        REMARK     TYPE AD_REMARK1,         " 注释
        BUKRS      TYPE T001-BUKRS,         " 公司代码
        AKONT      TYPE KNB1-AKONT,         " 统驭科目
        ZTERM      TYPE KNB1-ZTERM,         " 公司付款条件
        TEXT       TYPE C LENGTH 1000,      " 公司文本(部门)
        VKORG      TYPE RF02D-VKORG,        " 销售组织
        VTWEG      TYPE RF02D-VTWEG,        " 分销渠道
        SPART      TYPE RF02D-SPART,        " 产品组
        BZIRK      TYPE KNVV-BZIRK,         " 销售区域
        VKBUR      TYPE KNVV-VKBUR,         " 销售部门
        WAERS      TYPE KNVV-WAERS,         " 币种
        KONDA      TYPE KNVV-KONDA,         " 价格组
        KALKS      TYPE KNVV-KALKS,         " 定价过程
        VWERK      TYPE KNVV-VWERK,         " 交货工厂
        VSBED      TYPE KNVV-VSBED,         " 装运条件
        ZTERM2     TYPE KNB1-ZTERM,         " 销售付款条件
        KTGRD      TYPE KNVV-KTGRD,         " 客户分配账户组别
        TAXKD      TYPE KNVI-TAXKD,         " 税分类
        ICON       TYPE C LENGTH 4,         " 预警信号灯
        MESSAGE    TYPE C LENGTH 360,       " 信息
        BOX        TYPE C,
      END OF TY_INPUT.
DATA:GT_INPUT TYPE STANDARD TABLE OF TY_INPUT,
     GS_INPUT TYPE TY_INPUT.
DATA:GT_ALV TYPE STANDARD TABLE OF TY_INPUT,
     GS_ALV TYPE TY_INPUT.
*----------------------------------------------------------------------*
* 数据对象
*----------------------------------------------------------------------*
DATA:FUNCTXT_01 TYPE SMP_DYNTXT.
*----------------------------------------------------------------------*
* 选择屏幕
*----------------------------------------------------------------------*
SELECTION-SCREEN BEGIN OF BLOCK BK1 WITH FRAME TITLE TEXT-001.
PARAMETERS:P_FILE TYPE CHAR128.     " 选择文件
SELECTION-SCREEN:FUNCTION KEY 1.    " 下载模板按钮
SELECTION-SCREEN END OF BLOCK BK1.

************************************************************************
* I N I T I A L I Z A T I O N
************************************************************************
INITIALIZATION.
  PERFORM frm_init_event.

AT SELECTION-SCREEN ON VALUE-REQUEST FOR p_file.
  PERFORM f4_file CHANGING p_file .


*************************************************************************
** A T   S E L E C T I O N   S C R E E N   P A I
*************************************************************************
AT SELECTION-SCREEN.
  IF sscrfields-ucomm = 'FC01'.
    PERFORM frm_download.
  ENDIF.


************************************************************************
* S T A R T   O F   S E L E C T I O N
************************************************************************
START-OF-SELECTION.
  PERFORM frm_check_para.
  PERFORM frm_upload.


************************************************************************
* E N D   O F   S E L E C T I O N
************************************************************************
END-OF-SELECTION.
  PERFORM FRM_SET_LAYOUT.
  PERFORM FRM_SET_FIELDCAT.
  PERFORM FRM_DISPLAY_DATA.



*&---------------------------------------------------------------------*
*& Form FRM_INIT_EVENT
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM FRM_INIT_EVENT .
  FUNCTXT_01-ICON_ID = ICON_MAPPED_RELATION.
  FUNCTXT_01-ICON_TEXT = '下载模板文件'.
  SSCRFIELDS-FUNCTXT_01 = FUNCTXT_01.
ENDFORM.

*&---------------------------------------------------------------------*
*&      Form  F4_FILE
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*      -->P_0010   text
*----------------------------------------------------------------------*
FORM F4_FILE CHANGING P_10 .
  DATA:IT_FILETABLE TYPE FILETABLE,
       L_RC         TYPE I.

* 打开文件对话
  CALL METHOD CL_GUI_FRONTEND_SERVICES=>FILE_OPEN_DIALOG
    EXPORTING
      WINDOW_TITLE            = '请选择导入文件'                           " 默认名称'请选择导入文件'
      FILE_FILTER             = CL_GUI_FRONTEND_SERVICES=>FILETYPE_EXCEL   " 默认文件后缀名
      MULTISELECTION          = SPACE            "
    CHANGING
      FILE_TABLE              = IT_FILETABLE     "导入数据放入内表
      RC                      = L_RC             "返回正误判断
    EXCEPTIONS
      FILE_OPEN_DIALOG_FAILED = 1
      CNTL_ERROR              = 2
      ERROR_NO_GUI            = 3
      NOT_SUPPORTED_BY_GUI    = 4
      OTHERS                  = 5.

* 打开文件对话成功的情况下
  IF SY-SUBRC = 0 AND L_RC = 1.
*   读取内表
    READ TABLE IT_FILETABLE INTO P_10 INDEX 1.
*   因为执行成功，读取第一行一定可以读到
    IF SY-SUBRC NE 0.
      CLEAR P_10.
    ENDIF.
  ENDIF.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form FRM_DOWNLOAD
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM FRM_DOWNLOAD .
  DATA: P_OBJID(20) TYPE C.  "文件名
  DATA: LV_FNAME TYPE STRING,
        LV_TITLE TYPE STRING,
        LV_PATH  TYPE STRING VALUE 'D:/',
        LV_FPATH TYPE STRING VALUE 'D:/'.

  DATA: GS_WDATB   LIKE WWWDATATAB.
  DATA: LV_RC   TYPE SY-SUBRC.
  DATA: GV_MSG TYPE STRING .

  P_OBJID = 'ZSD001'.   "服务器中的文件名


  LV_FNAME = '客户批导导入模板'."默认文件名

  CONCATENATE LV_FNAME '下载' INTO LV_TITLE.

  CALL METHOD CL_GUI_FRONTEND_SERVICES=>FILE_SAVE_DIALOG
    EXPORTING
      WINDOW_TITLE              = LV_TITLE
      DEFAULT_EXTENSION         = 'XLS'
      DEFAULT_FILE_NAME         = LV_FNAME
"     with_encoding             =
      FILE_FILTER               = 'EXCEL文件(*.XLS)|*.XLS|全部文件 (*.*)|*.*|'
      INITIAL_DIRECTORY         = 'D:\'
      PROMPT_ON_OVERWRITE       = 'X'
    CHANGING
      FILENAME                  = LV_FNAME  "默认文件名称
      PATH                      = LV_PATH   "文件路径
      FULLPATH                  = LV_FPATH  "文件路径
"     user_action               =
"     file_encoding             =
    EXCEPTIONS
      CNTL_ERROR                = 1
      ERROR_NO_GUI              = 2
      NOT_SUPPORTED_BY_GUI      = 3
      INVALID_DEFAULT_FILE_NAME = 4
      OTHERS                    = 5.
  IF SY-SUBRC <> 0.
*   Implement suitable error handling here
  ELSE.
    SELECT SINGLE
                RELID
                OBJID
    FROM WWWDATA
    INTO CORRESPONDING FIELDS OF GS_WDATB
    WHERE SRTF2 = 0
    AND RELID = 'MI'        "对象类型，MI代表EXCEL
    AND OBJID = P_OBJID.    "服务器中上传的对象名
    IF GS_WDATB IS INITIAL.
      MESSAGE '模板文件不存在！' TYPE 'E'.
    ELSE.
      P_FILE = LV_FPATH.
      IF P_FILE IS NOT INITIAL.
        CALL FUNCTION 'DOWNLOAD_WEB_OBJECT'
          EXPORTING
            KEY         = GS_WDATB
            DESTINATION = P_FILE
          IMPORTING
            RC          = LV_RC.
        IF LV_RC NE 0.
          MESSAGE '模板下载失败！' TYPE 'E'.
        ELSE.
          CLEAR GV_MSG.
          CONCATENATE '模板下载到本地文件' P_FILE INTO GV_MSG.
          MESSAGE GV_MSG TYPE 'S' .
        ENDIF.
      ENDIF.
    ENDIF.
  ENDIF.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form FRM_CHECK_PARA
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM FRM_CHECK_PARA .
  IF P_FILE IS INITIAL .
    MESSAGE I999 WITH '路径未选择！' DISPLAY LIKE 'E' .
    LEAVE LIST-PROCESSING.
  ENDIF.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form FRM_UPLOAD
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM FRM_UPLOAD .
  DATA: T_RAW TYPE TRUXS_T_TEXT_DATA.

  "函数TEXT_CONVERT_XLS_TO_SAP将转化的内表放到GT_INPUT中
  CALL FUNCTION 'TEXT_CONVERT_XLS_TO_SAP'
    EXPORTING
      I_LINE_HEADER        = 'X'
      I_TAB_RAW_DATA       = T_RAW
      I_FILENAME           = P_FILE
    TABLES
      I_TAB_CONVERTED_DATA = GT_INPUT
    EXCEPTIONS
      CONVERSION_FAILED    = 1
      OTHERS               = 2.
  IF SY-SUBRC <> 0.
    MESSAGE ID SY-MSGID TYPE 'S' NUMBER SY-MSGNO WITH SY-MSGV1 SY-MSGV2 SY-MSGV3 SY-MSGV4 DISPLAY LIKE 'E'.
  ENDIF.
  LOOP AT GT_INPUT INTO GS_INPUT.
    CLEAR GS_ALV.
    MOVE-CORRESPONDING GS_INPUT  TO GS_ALV.
    APPEND GS_ALV TO GT_ALV.
    CLEAR GS_INPUT.
  ENDLOOP.
ENDFORM.
*&---------------------------------------------------------------------*
*& Form FRM_SET_LAYOUT
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM FRM_SET_LAYOUT .
  CLEAR LAYOUT.
  LAYOUT-BOX_FNAME  = 'BOX'.
  LAYOUT-SEL_MODE   = 'A'.     "选择行模式
  LAYOUT-CWIDTH_OPT = 'X'.     "优化列宽设置
  LAYOUT-ZEBRA      = 'X'.     "设置斑马线
ENDFORM.
*&---------------------------------------------------------------------*
*& Form FRM_SET_FIELDCAT
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM FRM_SET_FIELDCAT .
  CLEAR:GS_FIELDCAT,GT_FIELDCAT.
*&--field宏设置
  DEFINE FC.
    CLEAR gs_fieldcat.
    gs_fieldcat-fieldname  = &1.
    gs_fieldcat-scrtext_l  = &2.
    gs_fieldcat-ref_table  = &3.
    gs_fieldcat-ref_field  = &4.
    gs_fieldcat-no_zero    = &5.
    APPEND gs_fieldcat TO gt_fieldcat.
  END-OF-DEFINITION.
  FC 'ICON' '预警信号灯'      '' '' ''.
  FC 'BU_GROUP' '分组'       '' '' ''.
  FC 'BU_PARTNER' '合作伙伴号'           '' '' 'X'.
  FC 'BPEXT' '外部客户号'        '' '' ''.
  FC 'NAME_ORG1' '名称1'        '' '' ''.
  FC 'NAME_ORG2' '名称2'   '' '' ''.
  FC 'NAME_ORG3' '名称3'       '' '' ''.
  FC 'NAME_ORG4' '名称4'         '' '' ''.
  FC 'BU_SORT1' '搜索项1'       '' '' ''.
  FC 'BU_SORT2' '搜索项2'       '' '' 'X'.
  FC 'POST_CODE1' '邮政编码'           '' '' ''.
  FC 'REGION' '省份' '' '' ''.
  FC 'CITY' '城市'    '' '' ''.
  FC 'COUNTRY' '国家'          '' '' ''.
  FC 'STREET1' '街道1'  '' '' ''.
  FC 'STREET2' '街道2'        '' '' ''.
  FC 'LANGU' '语言'          '' '' ''.
  FC 'REMARK' '注释'        '' '' ''.
  FC 'BUKRS' '公司代码'        '' '' ''.
  FC 'AKONT' '统驭科目'        '' '' ''.
  FC 'ZTERM' '付款条件'        '' '' ''.
  FC 'TEXT' '文本(部门)'        '' '' ''.
  FC 'VKORG' '销售组织'        '' '' ''.
  FC 'VTWEG' '分销渠道'        '' '' ''.
  FC 'SPART' '产品组'        '' '' ''.
  FC 'BZIRK' '销售区域'        '' '' ''.
  FC 'VKBUR' '销售部门'        '' '' ''.
  FC 'WAERS' '币种'        '' '' ''.
  FC 'KONDA' '价格组'        '' '' ''.
  FC 'KALKS' '定价过程'        '' '' ''.
  FC 'VWERK' '交货工厂'        '' '' ''.
  FC 'VSBED' '装运条件'        '' '' ''.
  FC 'ZTERM2' '付款条件'        '' '' ''.
  FC 'KTGRD' '客户分配帐户组别'        '' '' ''.
  FC 'TAXKD' '税分类'        '' '' ''.
  FC 'MESSAGE' '消息'            '' '' ''.
ENDFORM.
*&---------------------------------------------------------------------*
*& Form FRM_DISPLAY_DATA
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM FRM_DISPLAY_DATA .

  CALL FUNCTION 'REUSE_ALV_GRID_DISPLAY_LVC'
    EXPORTING
      I_CALLBACK_PROGRAM       = SY-REPID
      I_CALLBACK_PF_STATUS_SET = 'FRM_SET_STATUS'
      I_CALLBACK_USER_COMMAND  = 'FRM_USER_COMMAND'
      IS_LAYOUT_LVC            = LAYOUT
      IT_FIELDCAT_LVC          = GT_FIELDCAT
    TABLES
      T_OUTTAB                 = GT_ALV
    EXCEPTIONS
      PROGRAM_ERROR            = 1
      OTHERS                   = 2.
  IF SY-SUBRC <> 0.
* Implement suitable error handling here
  ENDIF.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form FRM_SET_STATUS
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM FRM_SET_STATUS  USING RT_EXTAB TYPE SLIS_T_EXTAB.
  SET PF-STATUS 'STANDARD_FULLSCREEN' .
ENDFORM.
*&---------------------------------------------------------------------*
*& Form FRM_USER_COMMAND
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM FRM_USER_COMMAND USING  R_UCOMM LIKE SY-UCOMM
                         RS_SELFIELD TYPE SLIS_SELFIELD.
  "实时更新内表数据
  DATA:REF_GRID TYPE REF TO CL_GUI_ALV_GRID.

  CALL FUNCTION 'GET_GLOBALS_FROM_SLVC_FULLSCR'
    IMPORTING
      E_GRID = REF_GRID.                    " 获取全局变量
  CASE R_UCOMM.
    WHEN 'CREATE'.
      PERFORM FRM_CALL_BAPI.
  ENDCASE.
  "刷新ALV
  CALL FUNCTION 'GET_GLOBALS_FROM_SLVC_FULLSCR'
    IMPORTING
      E_GRID = REF_GRID.                    " 获取全局变量

  CALL METHOD REF_GRID->CHECK_CHANGED_DATA. " 获取响应事件
  RS_SELFIELD-REFRESH = 'X'.
ENDFORM.


*&---------------------------------------------------------------------*
*& Form FRM_CALL_BAPI
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM FRM_CALL_BAPI .
  DATA:V_PARTNER      LIKE BAPIBUS1006_HEAD-BPARTNER,
       V_PARTNER_GUID TYPE BU_PARTNER_GUID,
       V_PARTNER_ADDR TYPE AD_ADDRNUM,
       V_VENDOR_ADDR  TYPE AD_ADDRNUM.
  LOOP AT GT_ALV INTO GS_ALV WHERE BOX = 'X'.
    " 创建合作伙伴主体数据
    PERFORM FRM_CREATE_PARTNER USING GS_ALV CHANGING V_PARTNER V_PARTNER_GUID.
    IF V_PARTNER IS NOT INITIAL.
      " 添加合作伙伴角色
      PERFORM FRM_ADD_ROLE USING V_PARTNER  GS_ALV.
      "调用CMD_EI_API,添加外部客户
      PERFORM F_CALL_CMD_API USING V_PARTNER GS_ALV.
    ENDIF.
    GS_ALV-BU_PARTNER = V_PARTNER.
    MODIFY GT_ALV FROM GS_ALV.
  ENDLOOP.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form FRM_CREATE_PARTNER
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM FRM_CREATE_PARTNER USING P_GS_ALV STRUCTURE GS_ALV
                         CHANGING BUSINESSPARTNER PARTNER_GUID.
  DATA:BUSINESSPARTNEREXTERN   TYPE BAPIBUS1006_HEAD-BPARTNER,
       PARTNERCATEGORY         TYPE BAPIBUS1006_HEAD-PARTN_CAT,
       PARTNERGROUP            TYPE BAPIBUS1006_HEAD-PARTN_GRP,
       CENTRALDATA             TYPE BAPIBUS1006_CENTRAL,
       CENTRALDATAORGANIZATION TYPE BAPIBUS1006_CENTRAL_ORGAN,
       ADDRESSDATA             TYPE BAPIBUS1006_ADDRESS.
  " DATA:BUSINESSPARTNER TYPE BAPIBUS1006_HEAD-BPARTNER.
  DATA:TELEFONDATA     TYPE STANDARD TABLE OF BAPIADTEL,
       GS_TELEFONDATA  TYPE BAPIADTEL,
       E_MAILDATA      TYPE STANDARD TABLE OF BAPIADSMTP,
       GS_E_MAILDATA   TYPE BAPIADSMTP,
       ADDRESSNOTES    TYPE STANDARD TABLE OF BAPIAD_REM,
       GS_ADDRESSNOTES TYPE BAPIAD_REM,
       RETURN          TYPE STANDARD TABLE OF BAPIRET2,
       GS_RETURN       TYPE BAPIRET2.
  " 外部客户号:如果您需要在外部分配号码，您可以在创建业务伙伴时提供号码,在内部号码分配的情况下，您将此参数留空。
  BUSINESSPARTNEREXTERN = ''.
  " 业务伙伴类别（1代表人员，2代表组织，3代表组）
  PARTNERCATEGORY = '2'.
  " 业务伙伴分组:创建业务伙伴时，在“分组”字段中输入的内容决定业务伙伴号码字段是否以及如何填写数据。
  PARTNERGROUP = P_GS_ALV-BU_GROUP.

*&--检索项信息
  " 搜索项1
  CENTRALDATA-SEARCHTERM1 = P_GS_ALV-BU_SORT1.
  " 搜索项2
  CENTRALDATA-SEARCHTERM1 = P_GS_ALV-BU_SORT1.
  " 外部业务伙伴编号
  CENTRALDATA-PARTNEREXTERNAL = P_GS_ALV-BPEXT.
*&--名称信息
  "名称1
  CENTRALDATAORGANIZATION-NAME1 = P_GS_ALV-NAME_ORG1.
  "名称2
  CENTRALDATAORGANIZATION-NAME2 = P_GS_ALV-NAME_ORG2.
  "名称3
  CENTRALDATAORGANIZATION-NAME3 = P_GS_ALV-NAME_ORG3.
  "名称4
  CENTRALDATAORGANIZATION-NAME4 = P_GS_ALV-NAME_ORG4.
*&--标准地址
  " 邮政编码
  ADDRESSDATA-POSTL_COD1 = P_GS_ALV-POST_CODE1.
  " 城市
  ADDRESSDATA-CITY = P_GS_ALV-CITY.
  " 国家
  ADDRESSDATA-COUNTRY = P_GS_ALV-COUNTRY.
  " 省份/地区
  ADDRESSDATA-REGION  = P_GS_ALV-REGION.
  " 街道1
  ADDRESSDATA-STREET = P_GS_ALV-STREET1.
  " 街道2
  ADDRESSDATA-STR_SUPPL1 = P_GS_ALV-STREET2.
  "语言代码
  ADDRESSDATA-LANGU      = '1'.
*&--通信
  "语言代码
  GS_ADDRESSNOTES-LANGU      = '1'.
  " 地址注释
  GS_ADDRESSNOTES-ADR_NOTES = P_GS_ALV-REMARK.
  APPEND  GS_ADDRESSNOTES TO ADDRESSNOTES.

  CALL FUNCTION 'BAPI_BUPA_CREATE_FROM_DATA'
    EXPORTING
      BUSINESSPARTNEREXTERN   = BUSINESSPARTNEREXTERN
      PARTNERCATEGORY         = PARTNERCATEGORY
      PARTNERGROUP            = PARTNERGROUP
      CENTRALDATA             = CENTRALDATA
      CENTRALDATAORGANIZATION = CENTRALDATAORGANIZATION
      ADDRESSDATA             = ADDRESSDATA
    IMPORTING
      BUSINESSPARTNER         = BUSINESSPARTNER
    TABLES
*     TELEFONDATA             = TELEFONDATA
*     FAXDATA                 =
*     TELETEXDATA             =
*     TELEXDATA               =
*     E_MAILDATA              = E_MAILDATA
      ADDRESSNOTES            = ADDRESSNOTES
      RETURN                  = RETURN
*     ADDRESSDUPLICATES       =
    .
  READ TABLE RETURN INTO GS_RETURN WITH KEY TYPE = 'E'.
  IF SY-SUBRC = 0.
    CALL FUNCTION 'BAPI_TRANSACTION_ROLLBACK'.
    READ TABLE RETURN INTO GS_RETURN WITH KEY TYPE = 'E'.
    IF SY-SUBRC  = 0.
      GS_ALV-ICON = ICON_RED_LIGHT.
      GS_ALV-MESSAGE = GS_RETURN-MESSAGE.
      MODIFY GT_ALV FROM GS_ALV.
    ENDIF.

  ELSE.
    CALL FUNCTION 'BUPA_NUMBERS_GET'
      EXPORTING
        IV_PARTNER      = BUSINESSPARTNER
      IMPORTING
        EV_PARTNER_GUID = PARTNER_GUID.
  ENDIF.
  CLEAR:BUSINESSPARTNEREXTERN,PARTNERCATEGORY,PARTNERGROUP,CENTRALDATA,CENTRALDATAORGANIZATION,ADDRESSDATA.
  CLEAR:GS_ADDRESSNOTES,ADDRESSNOTES,GS_RETURN,RETURN.
ENDFORM.
*&---------------------------------------------------------------------*
*& Form FRM_ADD_ROLE
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM FRM_ADD_ROLE USING P_PARTNER P_GS_ALV STRUCTURE GS_ALV.
  DATA:RETURN TYPE STANDARD TABLE OF BAPIRET2.

*  IF P_GS_ALV-BUKRS IS NOT INITIAL.

  CALL FUNCTION 'BAPI_BUPA_ROLE_ADD'
    EXPORTING
      BUSINESSPARTNER     = P_PARTNER
      BUSINESSPARTNERROLE = 'FLCU00'   "FI客户
    TABLES
      RETURN              = RETURN.

*  READ TABLE RETURN TRANSPORTING NO FIELDS WITH KEY TYPE = 'E'.
*  IF SY-SUBRC = 0.
**      IF P_RD2 NE 'X'."扩展客户数据 不获取错误信息
**        M_COLL_MSG 'E' '999'  P_PARTNER '添加联系人角色错误' '' ''.
**      ENDIF.
*  ELSE.
*    CALL FUNCTION 'BAPI_TRANSACTION_COMMIT'
*      EXPORTING
*        WAIT = 'X'.
*  ENDIF.
*  ENDIF.
*  IF P_GS_ALV-VKORG IS NOT INITIAL.

  IF P_GS_ALV-BU_GROUP <> 'Y002'.
    CALL FUNCTION 'BAPI_BUPA_ROLE_ADD'
      EXPORTING
        BUSINESSPARTNER     = P_PARTNER
        BUSINESSPARTNERROLE = 'FLCU01'   "客户
      TABLES
        RETURN              = RETURN.
  ENDIF.
  READ TABLE RETURN TRANSPORTING NO FIELDS WITH KEY TYPE = 'E'.
  IF SY-SUBRC = 0.
    GS_ALV-ICON = ICON_RED_LIGHT.
    GS_ALV-MESSAGE = '角色添加失败'.
    MODIFY GT_ALV FROM GS_ALV.
  ELSE.
    CALL FUNCTION 'BAPI_TRANSACTION_COMMIT'
      EXPORTING
        WAIT = 'X'.
  ENDIF.
*  ENDIF.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form F_CALL_CMD_API
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*      -->P_V_PARTNER  text
*      -->P_GS_ALV  text
*&---------------------------------------------------------------------*
FORM F_CALL_CMD_API  USING    P_PARTNER
                              P_GS_ALV STRUCTURE GS_ALV.
  DATA:IS_MASTER_DATA           TYPE CMDS_EI_MAIN,
       L_EXTERN                 TYPE CMDS_EI_EXTERN,
       ES_MASTER_DATA_CORRECT   TYPE CMDS_EI_MAIN,
       ES_MESSAGE_CORRECT       TYPE CVIS_MESSAGE,
       ES_MASTER_DATA_DEFECTIVE TYPE CMDS_EI_MAIN,
       ES_MESSAGE_DEFECTIVE     TYPE CVIS_MESSAGE,
       GS_RETURN                TYPE BAPIRET2.
  CLEAR:IS_MASTER_DATA ,L_EXTERN .
  DATA:GS_CVI_CUST_LINK TYPE CVI_CUST_LINK.
*&--1.1外部接口：客户数据表头
  L_EXTERN-HEADER-OBJECT_INSTANCE-KUNNR = P_PARTNER.
  L_EXTERN-HEADER-OBJECT_TASK   = 'M' .            "I 创建 ;U 更改 ;M 创建/更改 ;C  当前状态
*&--1.2外部界面：中心数据
  PERFORM FRM_PREPARE_PARTNER USING GS_ALV CHANGING L_EXTERN .
  IF GS_ALV-BUKRS IS NOT INITIAL.
*&--1.3外部接口：公司代码数据
    PERFORM FRM_PREPARE_BUKRS USING GS_ALV CHANGING L_EXTERN .
  ENDIF.
  IF GS_ALV-VKORG IS NOT INITIAL.
*&--1.4外部接口：销售数据
    PERFORM FRM_PREPARE_VKORG USING P_PARTNER GS_ALV CHANGING L_EXTERN .
  ENDIF.
*&--添加数据进入IS_MASTER_DATA-CUSTOMERS内表中
  APPEND L_EXTERN TO IS_MASTER_DATA-CUSTOMERS .

*   Initialize all the data
  CMD_EI_API=>INITIALIZE( ).

  CALL METHOD CMD_EI_API=>MAINTAIN_BAPI
    EXPORTING
      IV_TEST_RUN              = SPACE " 'X'
      IV_COLLECT_MESSAGES      = 'X'
      IS_MASTER_DATA           = IS_MASTER_DATA
    IMPORTING
      ES_MASTER_DATA_CORRECT   = ES_MASTER_DATA_CORRECT "LO_CMDS_CORRECT
      ES_MESSAGE_CORRECT       = ES_MESSAGE_CORRECT "LO_CVIS_MSG_CORRECT
      ES_MASTER_DATA_DEFECTIVE = ES_MASTER_DATA_DEFECTIVE "LO_CMDS
      ES_MESSAGE_DEFECTIVE     = ES_MESSAGE_DEFECTIVE. "LO_CVIS_MSG_ERROR.
  IF ES_MESSAGE_DEFECTIVE-IS_ERROR IS INITIAL.
    CALL FUNCTION 'BAPI_TRANSACTION_COMMIT'
      EXPORTING
        WAIT = 'X'.
    SELECT SINGLE * INTO  GS_CVI_CUST_LINK FROM CVI_CUST_LINK WHERE CUSTOMER = P_PARTNER.
    IF SY-SUBRC = 0.
      GS_ALV-ICON = ICON_GREEN_LIGHT.
      GS_ALV-MESSAGE = '创建成功'.
      MODIFY GT_ALV FROM GS_ALV.
    ENDIF.

  ELSE.
    GS_ALV-ICON = ICON_RED_LIGHT.
    LOOP AT ES_MESSAGE_DEFECTIVE-MESSAGES INTO GS_RETURN WHERE TYPE = 'E'.
      CONCATENATE GS_RETURN-MESSAGE GS_ALV-MESSAGE INTO GS_ALV-MESSAGE.
    ENDLOOP.
    MODIFY GT_ALV FROM GS_ALV.
  ENDIF.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form FRM_PREPARE_PARTNER
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*      -->P_GS_ALV  text
*      <--P_L_EXTERN  text
*&---------------------------------------------------------------------*
FORM FRM_PREPARE_PARTNER  USING P_GS_ALV STRUCTURE GS_ALV P_EXTERN TYPE CMDS_EI_EXTERN.
  DATA:L_EXTERN         TYPE CMDS_EI_EXTERN.
  DATA:GS_TBD001 TYPE TBD001.
*&--外部接口：中心客户数据
  " 需要获取账户组
  CLEAR GS_TBD001.
  SELECT SINGLE * INTO GS_TBD001 FROM TBD001 WHERE BU_GROUP = P_GS_ALV-BU_GROUP.
  P_EXTERN-CENTRAL_DATA-CENTRAL-DATA-KTOKD =  GS_TBD001-KTOKD . "客户账户组
  P_EXTERN-CENTRAL_DATA-CENTRAL-DATAX-KTOKD = 'X' .
*&--外部接口：税收标识
  P_EXTERN-CENTRAL_DATA-TAX_IND-TAX_IND = VALUE CMDS_EI_CMD_TAX_IND-TAX_IND( (
  TASK = 'M'
  DATA_KEY-ALAND = GS_ALV-COUNTRY
  DATA_KEY-TATYP = 'MWST'
  DATA-TAXKD =   GS_ALV-TAXKD  " 客户税分类
  DATAX-TAXKD =   'X'          " 客户税分类
  ) ).
*&----外部接口：组织地址
  P_EXTERN-CENTRAL_DATA-ADDRESS-POSTAL-DATA-NAME    = GS_ALV-NAME_ORG1  .
  P_EXTERN-CENTRAL_DATA-ADDRESS-POSTAL-DATA-NAME_2  = GS_ALV-NAME_ORG2  .
  P_EXTERN-CENTRAL_DATA-ADDRESS-POSTAL-DATA-NAME_3  = GS_ALV-NAME_ORG3  .
  P_EXTERN-CENTRAL_DATA-ADDRESS-POSTAL-DATA-NAME_4  = GS_ALV-NAME_ORG4  .
  P_EXTERN-CENTRAL_DATA-ADDRESS-POSTAL-DATA-SORT1   = GS_ALV-BU_SORT1   .
  P_EXTERN-CENTRAL_DATA-ADDRESS-POSTAL-DATA-SORT2   = GS_ALV-BU_SORT2   .
  P_EXTERN-CENTRAL_DATA-ADDRESS-POSTAL-DATA-CITY    = GS_ALV-CITY.
  P_EXTERN-CENTRAL_DATA-ADDRESS-POSTAL-DATA-POSTL_COD1  = GS_ALV-POST_CODE1.
  P_EXTERN-CENTRAL_DATA-ADDRESS-POSTAL-DATA-COUNTRY     = GS_ALV-COUNTRY.
  P_EXTERN-CENTRAL_DATA-ADDRESS-POSTAL-DATA-COUNTRYISO  = GS_ALV-COUNTRY.
  P_EXTERN-CENTRAL_DATA-ADDRESS-POSTAL-DATA-STREET      = GS_ALV-STREET1.
  P_EXTERN-CENTRAL_DATA-ADDRESS-POSTAL-DATA-STREET_NO   = GS_ALV-STREET2.
  P_EXTERN-CENTRAL_DATA-ADDRESS-POSTAL-DATA-LANGU       = GS_ALV-LANGU.  " 中国

*  P_EXTERN-CENTRAL_DATA-ADDRESS-POSTAL-DATA-ADDR_VERS = 'C'.             " 国际地址的版本标识

  P_EXTERN-CENTRAL_DATA-ADDRESS-POSTAL-DATAX-NAME    = GS_ALV-NAME_ORG1  .
  P_EXTERN-CENTRAL_DATA-ADDRESS-POSTAL-DATAX-NAME_2  = GS_ALV-NAME_ORG2  .
  P_EXTERN-CENTRAL_DATA-ADDRESS-POSTAL-DATAX-NAME_3  = GS_ALV-NAME_ORG3  .
  P_EXTERN-CENTRAL_DATA-ADDRESS-POSTAL-DATAX-NAME_4  = GS_ALV-NAME_ORG4  .
  P_EXTERN-CENTRAL_DATA-ADDRESS-POSTAL-DATAX-SORT1   = GS_ALV-BU_SORT1   .
  P_EXTERN-CENTRAL_DATA-ADDRESS-POSTAL-DATAX-SORT2   = GS_ALV-BU_SORT2   .
  P_EXTERN-CENTRAL_DATA-ADDRESS-POSTAL-DATAX-CITY    = GS_ALV-CITY.
  P_EXTERN-CENTRAL_DATA-ADDRESS-POSTAL-DATAX-POSTL_COD1  = GS_ALV-POST_CODE1.
  P_EXTERN-CENTRAL_DATA-ADDRESS-POSTAL-DATAX-COUNTRY     = GS_ALV-COUNTRY.
  P_EXTERN-CENTRAL_DATA-ADDRESS-POSTAL-DATAX-COUNTRYISO  = GS_ALV-COUNTRY.
  P_EXTERN-CENTRAL_DATA-ADDRESS-POSTAL-DATAX-STREET      = GS_ALV-STREET1.
  P_EXTERN-CENTRAL_DATA-ADDRESS-POSTAL-DATAX-STREET_NO   = GS_ALV-STREET2.
  P_EXTERN-CENTRAL_DATA-ADDRESS-POSTAL-DATAX-LANGU       = GS_ALV-LANGU.  " 中国
ENDFORM.
*&---------------------------------------------------------------------*
*& Form FRM_PREPARE_BUKRS
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*      -->P_GS_ALV  text
*      <--P_L_EXTERN  text
*&---------------------------------------------------------------------*
FORM FRM_PREPARE_BUKRS  USING    P_GS_ALV STRUCTURE GS_ALV
                        CHANGING P_EXTERN TYPE CMDS_EI_EXTERN.
  DATA:GS_COMPANY TYPE CMDS_EI_COMPANY.
  DATA:GS_TEXTS TYPE CVIS_EI_CVIS_TEXT.
  DATA:GT_TEXT TYPE STANDARD TABLE OF CVIS_EI_TEXT,
       GS_TEXT TYPE CVIS_EI_TEXT.
  DATA:GT_DATA TYPE STANDARD TABLE OF TLINE,
       GS_DATA TYPE TLINE.
*&--外部接口:公司代码数据
*  P_EXTERN-COMPANY_DATA-COMPANY = VALUE CMDS_EI_CMD_COMPANY-COMPANY(
*    (
*      TASK = 'I'
*
*      DATA_KEY-BUKRS = GS_ALV-BUKRS   " 公司代码
*
*      DATA-AKONT = GS_ALV-AKONT       " 总帐中的统驭科目
*
*      DATA-ZTERM = GS_ALV-ZTERM       " 付款条件
**      DATA-LNRZE = PL_GOAL-LNRZE
**      DATA-ZUAWA = PL_GOAL-ZUAWA
**      DATA-REPRF = 'X'
**      DATA-ZWELS = PL_GOAL-ZWELS
*
*       DATAX-AKONT = 'X'              " 总帐中的统驭科目
*
*       DATAX-ZTERM = 'X'              " 付款条件
**      DATAX-LNRZE = 'X'
**      DATAX-ZUAWA = 'X'
**      DATAX-REPRF = 'X'
*
**      DATAX-ZWELS = 'X'
*    )
*   ) .
  GS_TEXT-TASK = 'M'.
  GS_TEXT-DATA_KEY-TEXT_ID = '0001'.
  GS_TEXT-DATA_KEY-LANGU = '1'.
  GS_TEXT-DATA_KEY-LANGUISO = ''.
  GS_DATA-TDFORMAT = ''.
  GS_DATA-TDLINE = GS_ALV-TEXT.
  APPEND GS_DATA TO GT_DATA.
  GS_TEXT-DATA = GT_DATA.
  APPEND GS_TEXT TO GT_TEXT.
  GS_TEXTS-TEXTS = GT_TEXT.
  GS_COMPANY-TEXTS = GS_TEXTS.
  GS_COMPANY-TASK = 'I'.
  GS_COMPANY-DATA_KEY-BUKRS = GS_ALV-BUKRS. " 公司代码
  GS_COMPANY-DATA-AKONT = GS_ALV-AKONT.
  GS_COMPANY-DATA-ZTERM = GS_ALV-ZTERM.
  GS_COMPANY-DATAX-AKONT = 'X'.
  GS_COMPANY-DATAX-ZTERM = 'X'.
  APPEND GS_COMPANY TO P_EXTERN-COMPANY_DATA-COMPANY.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form FRM_PREPARE_VKORG
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*      -->P_P_PARTNER  text
*      -->P_GS_ALV  text
*      <--P_L_EXTERN  text
*&---------------------------------------------------------------------*
FORM FRM_PREPARE_VKORG  USING    P_PARTNER
                                 P_GS_ALV STRUCTURE GS_ALV
                        CHANGING P_EXTERN TYPE CMDS_EI_EXTERN.
*&--外部接口：销售数据
*  DATA LC_WAERS TYPE WAERS.
*  SELECT SINGLE WAERS FROM T500W INTO LC_WAERS WHERE LAND1 = PL_GOAL-COUNTRY AND ENDDA = '99991231'.
  P_EXTERN-SALES_DATA-SALES = VALUE CMDS_EI_CMD_SALES-SALES(
    (

      TASK = 'I'

      DATA_KEY-VKORG = P_GS_ALV-VKORG "销售组织
      DATA_KEY-VTWEG = P_GS_ALV-VTWEG "分销渠道
      DATA_KEY-SPART = P_GS_ALV-SPART "产品组


      DATA-BZIRK = P_GS_ALV-BZIRK   " 销售地区
      DATA-VKBUR = P_GS_ALV-VKBUR   " 销售部门
      DATA-WAERS = GS_ALV-WAERS   " 币种
*     DATA-KDGRP = PL_GOAL-KDGRP  " 客户组
      DATA-KONDA = P_GS_ALV-KONDA   " 客户价格组
      DATA-KALKS = P_GS_ALV-KALKS   " 用于确定定价过程的客户分类
      DATA-VWERK = P_GS_ALV-VWERK   " 交货工厂
      DATA-VSBED = P_GS_ALV-VSBED   " 装运条件
      DATA-ZTERM = P_GS_ALV-ZTERM2   " 付款条件
      DATA-KTGRD = P_GS_ALV-KTGRD   " 客户组的帐户分配

*      DATA-AWAHR = '100'          " 该项目的订单可能性

      DATAX-BZIRK = 'X'
      DATAX-VKBUR = 'X'
      DATAX-WAERS = 'X'
      DATAX-KONDA = 'X'
      DATAX-KALKS = 'X'
      DATAX-VWERK = 'X'
      DATAX-VSBED = 'X'
      DATAX-ZTERM = 'X'
      DATAX-KTGRD = 'X'

      FUNCTIONS-FUNCTIONS = VALUE CMDS_EI_CMD_FUNCTIONS-FUNCTIONS(

       (
        TASK = 'M'
        DATA_KEY-PARVW = 'AG'
        DATA-PARTNER = P_PARTNER
        DATAX-PARTNER = 'X'

       )

     (
        TASK = 'M'
        DATA_KEY-PARVW = 'RE'
        DATA-PARTNER = P_PARTNER
        DATAX-PARTNER = 'X'

       )
       (
        TASK = 'M'
        DATA_KEY-PARVW = 'RG'
        DATA-PARTNER = P_PARTNER
        DATAX-PARTNER = 'X'

       )
       (
        TASK = 'M'
        DATA_KEY-PARVW = 'WE'
*        DATA-DEFPA = ''
        DATA-PARTNER = P_PARTNER
*        DATAX-DEFPA = 'X'
        DATAX-PARTNER = 'X'

       )
      )
    )
  ).
ENDFORM.